# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=cxxtools
pkgver=2.2.1
pkgrel=3
pkgdesc="Collection of general-purpose C++ classes"
url="http://www.tntnet.org"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('bash' 'gcc-libs' 'libnsl')
makedepends=('git')
source=("git+https://github.com/maekitalo/cxxtools.git#tag=v${pkgver}")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"

  ./configure --prefix=/usr \
              --disable-static \
              --disable-demos \
              --disable-unittest
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
